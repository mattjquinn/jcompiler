// See https://code.jsoftware.com/wiki/User:Marshall_Lochbaum/Formal_Parser
// for a formal parser description.

program = _{ SOI ~ "\n"* ~ (stmt ~ "\n"+) * ~ stmt? ~ EOI }

stmt = _{ expr }

expr = { assgmtExpr | monadicExpr | dyadicExpr | string | terms }
monadicExpr = { action ~ expr }
dyadicExpr = { (monadicExpr | terms) ~ action ~ expr }

assgmtExpr = { ident ~ "=:" ~ expr }
terms = { term+ }
term = _{ decimal | integer | ident | "(" ~ expr ~ ")" }

action = { verb ~ adverb* }
verb = { ">:" | "*:" | "-" | "%" | "#" | ">." | "," |
          "+" | "*" | "<"  | "=" | "^" | "|" | ">" | "$" }
adverb = { "/" }

// @ means "atomic": no implicit whitespace allowed.
integer = @{ "_"? ~ ASCII_DIGIT+ }
decimal = @{ "_"? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT* }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
string = @{ "'" ~ ( "''" | (!"'" ~ ANY) )* ~ "'" }

WHITESPACE = _{ " " | "\t" }

// Comments match up to, but not including, the next newline
// or end of file
COMMENT = _{ "NB." ~ ( !"\n" ~ ANY)* }