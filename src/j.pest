// See https://code.jsoftware.com/wiki/User:Marshall_Lochbaum/Formal_Parser
// for a formal parser description.

program = _{ SOI ~ (stmt ~ "\n") * ~ stmt? ~ EOI }

stmt = _{ expr }

expr = { insertExpr | assgmtExpr | monadicExpr | dyadicExpr | terms }
monadicExpr = { monadicVerb ~ expr }
dyadicExpr = { (monadicExpr | terms) ~ dyadicVerb ~ expr }

// To think: "/" is a dyad itself, can we write this more concisely?
insertExpr = { dyadicVerb ~ "/" ~ expr }

assgmtExpr = { ident ~ "=:" ~ expr }
terms = { term+ }
term = _{ number | ident | "(" ~ expr ~ ")" }

monadicVerb = { ">:" | "*:" | "-" }
dyadicVerb = { "+" | "*" | "-" | "<" | "=" | ">" }

// @ means "atomic": no implicit whitespace allowed.
number = @{ ASCII_DIGIT+ }
ident = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

WHITESPACE = _{ " " | "\t" }
COMMENT = _{ "NB." ~ (!"\n" ~ ANY)* ~ ("\n" | EOI) }